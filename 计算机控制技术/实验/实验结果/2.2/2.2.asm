CODE SEGMENT
ASSUME CS:CODE
START :MOV AX, OFFSET IRQ7;填写8259的7号中断矢量入口地址的偏移量
MOV SI,003CH ;填写8259中断7的中段矢量
MOV [SI],AX ;填偏移量矢量
MOV AX,CS ;填写8259中断矢量入口地址的段地址
MOV SI,003EH
MOV [SI],AX
CLI ;关系统总中断
CALL SYSINTI ;调用系统初始化子程序
MOV DX,0F043H ;定时器的TMRCON的地址
MOV AL,076H ;初始化1＃定时器定时1ms
OUT DX, AL
MOV DX,0F041H ;写1#定时器定时常数的低字节
MOV AL,0E8H
OUT DX,AL
MOV DX,0F041H ;写1#定时器定时常数的高字节
MOV AL,03H
OUT DX,AL
MOV CL,00H ;变量1、2=0
MOV BL,00H
AGAIN:STI ;打开系统总中断
HLT ;停机等待直到有中断产生
JMP AGAIN ;中断返回后继续
IRQ7: MOV AL,CL
MOV DX,0640H
OUT DX,AL
MOV AL,BL
MOV DX,0641H
OUT DX,AL
INC CL;变量1加一
JNZ FINISH ;变量1不等于0跳转
NOT BL;变量2取反
FINISH:MOV AL,20H ;写中断结束字
OUT 20H,AL
IRET ;中断返回
SYSINTI: MOV AX,8000H;系统初始化子程序
OUT 23H,AL ;扩展IO使能
XCHG AL,AH
OUT 22H,AL
OUT 22H,AX
MOV DX,0F822H ;初始化管脚配置P2CFG，配置CS0#
MOV AL,70H
OUT DX,AL
MOV DX,0F824H ;初始化管脚配置P3CFG,配置主片IRQ7
MOV AL,0B2H
OUT DX,AL
MOV DX,0F832H ;初始化管脚配置INTCFG
MOV AL,0AH
OUT DX,AL
MOV DX,0F834H ;初始化管脚配置TMRCFG
MOV AL,15H ;将GATE1接VCC
OUT DX,AL
MOV AL,11H ;初始化主片8259
OUT 20H,AL
MOV AL,08H
OUT 21H,AL
MOV AL,04H
OUT 21H,AL
MOV AL,01H
OUT 21H,AL
MOV AL,6FH ;写主片8259的中断屏蔽字，允许主片的
OUT 21H,AL
MOV AL,11H ;初始化从片8259
OUT 0A0H,AL
MOV AL,30H
OUT 0A1H,AL
MOV AL,02H
OUT 0A1H,AL
MOV AL,01H
OUT 0A1H,AL
MOV AL,0FFH
OUT 0A1H,AL
RET
CODE ENDS
END START