AD_ADDR EQU 0600H
DA1_ADDR EQU 0640H
STACK1 SEGMENT STACK
DW 256 DUP(?)
STACK1 ENDS
DATA SEGMENT
TK DB 06H ;采样周期
DATA ENDS
CODE SEGMENT
ASSUME CS:CODE, DS:DATA
START: MOV AX,DATA
MOV DS,AX
PUSH DS
MOV AX,0000H
MOV DS,AX
MOV AX,OFFSET IRQ7;填写8259的7号中断矢量入口地址的偏移量
MOV SI,003CH ;填写8259中断7的中段矢量
MOV [SI],AX ;填偏移量矢量
MOV AX,CS ;填写8259中断矢量入口地址的段地址
MOV SI,003EH ;填写7号中断段地址矢量
MOV [SI],AX
POP DS
CLI ;关系统总中断
CALL SYSINTI ;调用系统初始化子程序
MOV DX,0F043H
MOV AL,074H ;初始化1＃定时器定时10ms
OUT DX,AL
MOV DX,0F041H
MOV AL,10H ;写1#定时器定时常数的低字节
OUT DX,AL
MOV DX,0F041H
MOV AL,27H ;写1#定时器定时常数的高字节,同时启动AD转
换
OUT DX,AL
MOV BL,TK ;赋采样周期变量初值
AGAIN: STI ;打开系统总中断
HLT ;停机等待直到有中断产生
JMP AGAIN ;继续
IRQ7: DEC BL ;采样周期到否
JNZ FINISH ;采样周期没到，中断返回
MOV DX,AD_ADDR
IN AL,DX ;读AD采样值
MOV DX,DA1_ADDR
OUT DX,AL
MOV BL,TK
FINISH: MOV AL,20H ;中断结束
OUT 20H,AL
IRET ;中断返回
SYSINTI: MOV AX,8000H ;系统初始化子程序
OUT 23h,AL ;扩展IO使能
XCHG AL,AH
OUT 22H,AL
OUT 22H,AX
MOV DX,0F822H ;初始化管脚配置P2CFG，配置CS0#
MOV AL,70H
OUT DX,AL
MOV DX,0F824H ;初始化管脚配置P3CFG,配置主片IRQ7
MOV AL,0B2H
OUT DX,AL
MOV DX,0F832H ;初始化管脚配置INTCFG
MOV AL,0AH
OUT DX,AL
MOV DX,0F834H ;初始化管脚配置TMRCFG
MOV AL,15H ;将GATE1接VCC
OUT DX,AL
MOV AL,11H ;初始化主片8259
OUT 20H,AL
MOV AL,08H
OUT 21H,AL
MOV AL,04H
OUT 21H,AL
MOV AL,01H
OUT 21H,AL
MOV AL,6FH ;写主片8259的中断屏蔽字，允许主片的IRQ7
OUT 21H,AL
MOV AL,11H ;初始化从片8259
OUT 0A0H,AL
MOV AL,30H
OUT 0A1H,AL
MOV AL,02H
OUT 0A1H,AL
MOV AL,01H
OUT 0A1H,AL
MOV AL,0FFH
OUT 0A1H,AL
RET
CODE ENDS
END START