;最小拍算法（有纹波）
AD_ADDR EQU 0600H
DA1_ADDR EQU 0640H
DIN_ADDR EQU 0F860H
STACK1 SEGMENT STACK
DW 256 DUP(?)
STACK1 ENDS
DATA SEGMENT
SAVEBUFF DB 256 DUP(00H) ;存储缓冲
TK DB 64H ;采样周期
K0 DB 00H,54H,35H ;系数K0的值为+0.5435
K1 DB 01H,20H,00H ;系数K1的值为-0.2000
K2 DB 00H,00H,00H ;系数K2的值为0
K3 DB 00H,00H,00H ;系数K3的值为0
P1 DB 00H,71H,70H ;系数P1的值为+0.7180
P2 DB 00H,00H,00H ;系数P2的值为0
P3 DB 00H,00H,00H ;系数P3的值为0
SAVEMARK DB 00H ;存储缓冲标志
SAVEADDR DB 0FFH ;存储缓冲相加标志
CONTROLMARK DB 00H ;存储缓冲控制标志
EKB DB 00H ;偏差（字节）
K0_16 DB 00H,00H,00H ;系数K0转换二进制小数的变量
K1_16 DB 00H,00H,00H ;系数K1转换二进制小数的变量
K2_16 DB 00H,00H,00H ;系数K2转换二进制小数的变量
K3_16 DB 00H,00H,00H ;系数K3转换二进制小数的变量
P1_16 DB 00H,00H,00H ;系数P1转换二进制小数的变量
P2_16 DB 00H,00H,00H ;系数P2转换二进制小数的变量
P3_16 DB 00H,00H,00H ;系数P3转换二进制小数的变量
EK DW 0000H ;偏差（字）
EK_1 DW 0000H ;上一次偏差
EK_2 DW 0000H ;上上一次偏差
EK_3 DW 0000H ;上上上一次偏差
UK DW 0000H ;当前的控制量
UK_1 DW 0000H ;上一次的控制量
UK_2 DW 0000H ;上上一次控制量
UK_3 DW 0000H ;上上上一次的控制量
OUTPUT DW 0000H ;控制量输出的变量
ALLK_ALLP DW 0000H ;K1*EK_1＋K2*EK_2+K3*EK_3-P1*UK_1-P2*UK_2-P3*UK_3的值

DATA ENDS
CODE SEGMENT
ASSUME CS:CODE,DS:DATA
START: MOV AX,DATA ;填写数据段
MOV DS,AX
PUSH DS
XOR AX,AX ;填写7号中断入口地址
MOV DS,AX
MOV AX,OFFSET IRQ7
MOV SI,003CH
MOV [SI],AX
MOV AX,CS
MOV SI,003EH
MOV [SI],AX
CLI
POP DS
CALL SYSINTI ;系统初始化
MOV DX,0F043H ;初始化定时器1 定时10ms
MOV AL,076H
OUT DX,AL
MOV DX,0F041H
MOV AL,10H
OUT DX,AL
MOV DX,0F041H
MOV AL,27H
OUT DX,AL
IN AL,21H ;开7号中断
AND AL,7FH
OUT 21H,AL
MOV SI,OFFSET P3+2 ;系数转换成二进制小数
MOV BH,07H
MOV DI,OFFSET P3_16+2
CALL CHANGE
CALL CLEAR_E ;变量清零
MOV DX,DA1_ADDR
MOV AL,80H ;D/A0832置零
OUT DX,AL
MOV BL,01H
MOV SAVEMARK,00H

MOV SAVEADDR,0FFH
AGAIN: STI
HLT
JMP AGAIN
IRQ7: MOV DX,DIN_ADDR ;判同步信号到否
IN AL,DX
AND AL,01H
JZ ZZB
MOV DX,DA1_ADDR
MOV AL,80H ;D/A0832置零
OUT DX,AL
MOV CONTROLMARK,AL
MOV SAVEMARK,AL
CALL CLEAR_E
MOV BL,01H
JMP FINISH
ZZB: MOV AL,01H
MOV CONTROLMARK,AL
DEC BL
JNZ FINISH
CALL CY ;调用采样子程序
MOV EK,DX
MOV AX,DX ;计算K0*EK
MOV DI,OFFSET K0_16
INC DI
MOV DX,[DI]
CALL ML
MOV AX,ALLK_ALLP ;与变量ALLK_ALLP相加，得到控制量
ADD DX,AX
CALL OUT_PUT
MOV DX,OUTPUT
MOV UK,DX ;EK,EK_1,EK_2,EK_3,UK,UK_1,UK_2,UK_3变量递推
MOV SI,OFFSET UK_2
MOV DI,OFFSET UK_3
MOV BL,07H
L1: MOV AX,[SI]
MOV [DI],AX
DEC SI
DEC SI
DEC DI
DEC DI

DEC BL
JNZ L1
MOV AX,0000H ;计算K1*EK_1＋K2*EK_2+K3*EK_3-P1*UK_1-P2*UK_2-P3*UK_3
MOV ALLK_ALLP,AX
MOV SI,OFFSET K1_16
MOV DI,OFFSET EK_1
MOV CX,0003H
CALL L2
MOV SI,OFFSET P1_16
MOV DI,OFFSET UK_1
MOV CX,0103H
CALL L2
MOV BL,TK ;采样周期变量还原
FINISH: MOV AL,20H ;中断返回
OUT 20H,AL
IRET
L2: INC SI
MOV DX,[SI]
MOV AX,[DI]
CALL ML
DEC SI
TEST BYTE PTR[SI],01H
JNZ QB
LL: INC SI
INC SI
INC SI
INC DI
INC DI
MOV AX,ALLK_ALLP
CMP CH,00H
JNZ L3
ADD DX,AX
L4: MOV ALLK_ALLP,DX
DEC CL
JNZ L2
RET
L3: XCHG DX,AX
SUB DX,AX
JMP L4
QB: NEG DX
JMP LL

;乘法子程序,
;适用范围： 被乘数为符号数，乘数为无符号数，16位乘法
;参数： 被乘数AX，乘数DX
;返回值： 结果的高16位在DX中，低16位在AX中
ML: CMP DX,7FFFH
JA ML1
IMUL DX
RET
ML1: PUSH BX
PUSH CX
PUSH AX
SUB DX,7FFFH
IMUL DX
MOV BX,DX
MOV CX,AX
POP AX
MOV DX,7FFFH
IMUL DX
ADD AX,CX
ADC DX,BX
POP CX
POP BX
RET
;采样子程序
;功能： 从A/D读取采样值，并进行变换，采样值大于80H，表示0～－5V，采样值小于80H,表示0～＋5V
; 并计算偏差EK，分别将其扩大32倍，为了保证计算精度
;返回值： 返回值存在相应的变量中
CY: MOV DX,AD_ADDR
IN AL,DX
SUB AL,80H
MOV EKB,AL
CALL SAVEEK
MOV DX,0000H
MOV DH,EKB
CLC
MOV CL,03H
SAR DX,CL
RET
SAVEEK: MOV AH,AL
MOV DX,OFFSET SAVEBUFF
MOV AL,CONTROLMARK
CMP AL,00H

JZ L5
MOV AL,SAVEMARK
CMP AL,01H
JZ L5
MOV AL,SAVEADDR
CMP AL,0FEH
JZ L6
INC AL
MOV DL,AL
MOV SAVEADDR,AL
PUSH DI
MOV DI,DX
MOV [DI],AH
POP DI
L5: MOV AL,AH
RET
L6: INC SAVEMARK
JMP L5
;控制量输出子程序
;功能： 控制量判溢出，放大8倍后输出
OUT_PUT:MOV OUTPUT,DX
TEST DH,80H ;判溢出
JZ L7
MOV BX,0F000H
SUB DX,BX
JG L8
MOV DX,0F000H
JMP L9
L7: MOV BX,0FFFH
SUB DX,BX
JG L10
L8: MOV DX,OUTPUT
JMP L11
L10: MOV DX,0FFFH
L9: MOV OUTPUT,DX
L11: MOV CL,03H
MOV AL,CONTROLMARK
CMP AL,00H
JZ L12
SHL DX,CL
MOV AL,DH ;控制量输出
MOV DX,DA1_ADDR
ADD AL,80H
OUT DX,AL

L12: RET
;系数转换子程序
;功能： 将10进制表示的数转换成2进制小数，并保留其符号
;返回值; 转换结果保存在相应的变量中
CHANGE: DEC SI
MOV CX,[SI]
INC SI
AND AL,AL
MOV DX,0000H
MOV BL,10H
GO: MOV AL,[SI]
ADD AL,AL
DAA
MOV [SI],AL
DEC SI
MOV AL,[SI]
ADC AL,AL
DAA
RCL DX,0001H
MOV [SI],AL
INC SI
DEC BL
JNZ GO
DEC SI
MOV [SI],CX
DEC SI
MOV AL,[SI]
DEC DI
MOV [DI],DX
DEC SI
DEC DI
MOV [DI],AL
DEC DI
DEC BH
JNZ CHANGE
RET
;变量清零子程序
CLEAR_E:MOV AX,0000H
MOV ALLK_ALLP,AX
MOV SI,OFFSET EK
MOV BL,08H
GOON: MOV [SI],AX
INC SI
INC SI

DEC BL
JNZ GOON
RET
;延时子程序
;功能： 延时一段时间
;无返回值
DELAY: PUSH CX
MOV CX,1000H
DEL1: PUSH AX
POP AX
LOOP DEL1
POP CX
RET
;系统初始化子程序
;功能： 初始化IO,和CS0片选，管脚配置，中断
sysinti:MOV AX,8000h
OUT 23h,AL
XCHG AL,AH
OUT 22H,AL
OUT 22H,AX
MOV DX,0F822H
MOV AL,70H
OUT DX,AL
MOV DX,0F824H
MOV AL,0B2H
OUT DX,AL
MOV DX,0F832H
MOV AL,0AH
OUT DX,AL
MOV DX,0F834H
MOV AL,1DH
OUT DX,AL
MOV AL,11H
OUT 20H,AL
MOV AL,08H
OUT 21H,AL
MOV AL,04H
OUT 21H,AL
MOV AL,01H
OUT 21H,AL
MOV AL,0EFH
OUT 21H,AL
MOV AL,11H
OUT 0A0H,AL
MOV AL,30H
OUT 0A1H,AL
MOV AL,02H
OUT 0A1H,AL
MOV AL,01H
OUT 0A1H,AL
MOV AL,0FFH
OUT 0A1H,AL
RET
CODE ENDS
END START